group = 'com.github.aphoh'
version = '0.0.1-SNAPSHOT'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.7.7'
    }
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.google.protobuf'

description = """Pokemon Go Java API"""

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenCentral()
}

sourceSets {
    main {
        proto {
            // Need to use custom dir cause Gradle doesn't like us otherwise :(
            srcDir 'src/resources/protobuf/pogo'
            include '**/*.proto'
        }
    }
}

// Remove all .proto definition from the final build
processResources {
    exclude('**/*')
}

// Run this task to bundle all needed dependency
task bundle(type: Jar) {
    baseName = project.name + '_bundle'
    
    from {
    	configurations.compile.collect {
    		it.isDirectory() && !it.isEmpty() ? it : zipTree(it)
    	}
    }
    with jar
}

protobuf {
    // Configure the protoc executable
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:3.0.0-beta-3'
    }
}

dependencies {
    compile 'com.google.code.gson:gson:2.7'
    compile 'net.sourceforge.streamsupport:streamsupport:1.5'
    compile 'com.squareup.okhttp3:okhttp:3.4.0-RC1'
    compile 'com.google.protobuf:protobuf-java:3.0.0-beta-3'
    compileOnly "org.projectlombok:lombok:1.16.6"
}

idea {
    module {
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java");
    }
}