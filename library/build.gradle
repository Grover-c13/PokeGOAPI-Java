buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.7.7'
    }
}

apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'com.google.protobuf'

archivesBaseName = archivesBaseName + '-api'

sourceSets {
    main {
        proto {
            // Need to use custom dir cause Gradle doesn't like us otherwise :(
            srcDir 'src/resources/protobuf/src'
            include '**/*.proto'
        }
    }
}

// Remove all .proto definition from the final build
processResources {
    exclude('POGOProtos/')
    exclude('signature/Signature.proto')
}

protobuf {
    // Configure the protoc executable
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:3.0.0-beta-3'
    }
}

dependencies {
    compile 'com.squareup.okio:okio:1.9.0'
    compile 'com.squareup.moshi:moshi:1.2.0'
    compile 'com.annimon:stream:1.1.1'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.google.protobuf:protobuf-java:3.0.0-beta-3'
    compile 'io.reactivex:rxjava:1.1.8'
    compile 'net.jpountz.lz4:lz4:1.3.0'
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'org.projectlombok:lombok:1.16.10'
    testCompile 'junit:junit:4.10'
    testCompile 'ch.qos.logback:logback-classic:1.1.7'
}

idea {
    module {
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java");
    }
}